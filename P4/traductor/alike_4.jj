//*****************************************************************
// File:   alike.jj
// Author: Jes칰s L칩pez Ans칩n, 839922
// 		   Javier Sin Pelayo, 843442
// Date:   abril 2024
// Coms:   compilar mediante "ant"
// Doc:    https://javacc.github.io/javacc/
//*****************************************************************

options {
	IGNORE_CASE = true;
	UNICODE_INPUT = true;
	DEBUG_PARSER = false;
}

PARSER_BEGIN(alike_4)

package traductor;

import java.io.FileWriter;
import java.io.File;
import java.io.IOException;

import lib.symbolTable.*;
import lib.symbolTable.exceptions.*;
import lib.tools.SemanticFunctions.SemanticFunctions;
import lib.attributes.*;

import lib.tools.codeGeneration.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.LinkedHashMap;

public class alike_4 {

	static SemanticFunctions semantic; // Funciones sem치nticas

	static boolean verbose = false; // Flag modo verbose (impresi칩n bloque cerrado tabla de s칤mbolos)

   	public static void main(String[] args) {
	   	alike_4 parser = null;
		
		// argumento verbose
		if (args.length > 1 && args[1].equals("-v")) {
			verbose = true;
		}

		// creaci칩n de SemanticFuncions, donde se encapsula la l칩gica sem치ntica junto a la tabla de s칤mbolos
	   	semantic = new SemanticFunctions();
		String fileName = "";
	   
	   	try {
			if(args.length == 0) { // Si no se especifica fichero, se lee de la entrada est치ndar
				parser = new alike_4(System.in);
			}
			else {
				fileName = args[0];
				parser = new alike_4(new java.io.FileInputStream(fileName+".al"));
			}
			//Programa es el s칤mbolo inicial de la gram치tica
			parser.Programa(fileName);
			
			if (!semantic.hayErrores()) {
				fileName = fileName.substring(0, fileName.lastIndexOf('.')); // Eliminar extensi칩n
				System.out.println("Compilaci칩n finalizada. Se ha generado el fichero " + fileName + ".pcode");
			}
			else
				if (semantic.getNumErrores() == 1) {
					System.out.println("Detectado 1 error sem치ntico. No se genera c칩digo.");
				} else {
					System.out.println("Detectados " + semantic.getNumErrores() + " errores sem치nticos. No se genera c칩digo.");
				}
		}
		catch (java.io.FileNotFoundException e) {
			System.err.println ("Fichero " + fileName + ".al" +" no encontrado.");
		}
		catch (TokenMgrError e) {
			System.err.println("ERROR L칄XICO: " + e.getMessage());
		}
		catch (ParseException e) {
			System.err.println("----------------------------------------");
			System.err.println(e.getMessage());
		}
		catch (Exception e) {
			e.printStackTrace();
		}
   }
}


PARSER_END(alike_4)

//---------------------------------------------------------


SKIP : {
	" " | "\t" | "\n" | "\r" 
	|  <COMMENT: "--" (~["\n", "\r"])*> 
}

TOKEN : {
	< #LETTER: (["a"-"z","A"-"Z"]) > 
|	< #DIGIT: ["0"-"9"] >

	// Separadores
|	< tSEMICOLON: ";" >
|	< tCOLON: ":" >
|	< tCOMMA: "," >

	// Delimitadores
|	< tLPAREN: "(" >
|	< tRPAREN: ")" >

	// Tipos de datos
|	< tINT: "integer" >
|	< tCHAR: "character" >
|	< tBOOL: "boolean" >

|   < tARRAY: "array" >
| 	< tOF: "of" >
|	< tRANGE: ".." >

	// Palabras reservadas
|	< tWHILE: "while" > 
|   < tLOOP: "loop" >
|	< tIF: "if" >
|	< tTHEN: "then" >
|	< tELSIF: "elsif" >
|	< tELSE: "else" >

|   < tPROCEDURE: "procedure" >
|   < tFUNCTION: "function" >
|   < tREF: "ref" >
|   < tIS: "is" >
|   < tBEGIN: "begin" >
|   < tEND: "end" >
|	< tRETURN: "return" > 

	// Funciones est치ndar
| 	< tPUT: "put" >
| 	< tPUT_LINE: "put_line" >
|	< tGET: "get" >
|	< tSKIP_LINE: "skip_line" >
|	< tINT2CHAR: "int2char" >
|	< tCHAR2INT: "char2int" >
|	< tEXIT: "exit" >
|	< tNULL: "null" >

	// Literales
| 	< tTRUE: "true" >
| 	< tFALSE: "false" >
| 	< tCONST_INT: (<DIGIT>)+ >
|	< tCONST_CHAR: "'"~[]"'" >
|	< tCONST_STRING: "\"" ((~["\""] | "\"\""))* "\"" > // Dos comillas dobles seguidas se consideran una comilla doble
	{
		// Sustituir las comillas dobles por una comilla simple
		String str = matchedToken.image;
		str = str.substring(1, str.length() - 1); // Eliminar las comillas dobles de los extremos
		matchedToken.image = str.replace("\"\"", "\""); // Sustituir las parejas de comillas dobles por una sola ocurrencia
	}

	// Operadores
|	< tASSIGN: ":=" > 
|	< tAND: "and" >
|	< tOR: "or" >
|	< tNOT: "not" >
|	< tEQ: "=" >
|	< tNEQ: "/=" >
|	< tLT: "<" >
|	< tLE: "<=" >
|	< tGT: ">" >
|	< tGE: ">=" >
|	< tPLUS: "+" >
|	< tMINUS: "-" >
|	< tMULT: "*" >
|	< tDIV: "/" >
|	< tMOD: "mod" >

	// Otros
|	< tID: (<LETTER> | "_") (<LETTER> | <DIGIT> | "_")* >
}

SPECIAL_TOKEN : { // Debe estar al final para que sea lo 칰ltimo en comprobarse
 	< tBASURA: ~[] >
	 	{
			System.err.println("----------------------------------------");
			System.err.println(TokenMgrError.LexicalErr(matchedToken, "Car치cter extra침o: '" + matchedToken.image + "'. Reemplazo por espacio"));
			matchedToken.image = " ";
	 	}
}

//------------ S칤mbolo inicial de la gram치tica.
// Programa: <tPROCEDURE> <tID> <tIS> (declaracion_var)* (declaracion_procedimiento | declaracion_funcion)* <tBEGIN> instruccion+ <tEND> <tSEMICOLON> <EOF>
void Programa(String file) :
{
	Token id;
	CodeBlock programa;
	Attributes at = new Attributes(), at1 = new Attributes();
	String label;
}
{
	{ // Inicializaci칩n del programa
		label = CGUtils.newLabel();
		programa = new CodeBlock();
		programa.addInst(PCodeInstruction.OpCode.ENP, label); // Enter Program en 'label', definido justo arriba
	}
	<tPROCEDURE>
	id = <tID>
	{	// Procedimiento principal
		semantic.insertSymbol(id, new SymbolProcedure(id.image, new ArrayList<Symbol>()), label);
		semantic.setMainProcedureName(id.image);
	}
	<tIS>
		( declaracion_var(at) )*
		( declaracion_procedimiento(at) | declaracion_funcion(at) )*
	<tBEGIN>
		(instruccion(at1))+
	<tEND> <tSEMICOLON>
	{
		if (verbose) semantic.printSymbolTable(id.image); // Impresi칩n de la tabla de s칤mbolos
		programa.addBlock(at.code);
		programa.addLabel(label); // A침adir la etiqueta de inicio del programa
		programa.addBlock(at1.code);
		programa.addInst(PCodeInstruction.OpCode.LVP); // Leave Program
		if (!semantic.hayErrores()) {
			// file = file.substring(0, file.lastIndexOf('.')); // Eliminar extensi칩n
			File f = new File(file + ".pcode");
			System.out.println("Generando fichero " + file + ".pcode");
			try {
				FileWriter fw = new FileWriter(f);
				fw.write(programa.toString());
				fw.close();
			}
			catch (IOException e) {
				System.err.println("Error al abrir el fichero");
			}
		}
	}
}

//declaracion_var: lista_ids <tCOLON> tipo_variable <tSEMICOLON>
void declaracion_var(Attributes at) :
{
	TypeAttrib typeAt = new TypeAttrib();
}
{
	lista_ids(typeAt.t)
	<tCOLON>
	tipo_variable(typeAt)
	<tSEMICOLON> 
	{ at.code.addBlock(typeAt.code); }
}

//lista_ids: <tID> (<tCOMMA> <tID>)*
/**
 * Params:
 * 	-tokens: lista de tokens de la enumeraci칩n de los identificadores de las variables
 */
void lista_ids(List<Token> list) :
{
	Token t;
}
{
	t = <tID> {list.add(t);}
	(<tCOMMA> t = <tID> {list.add(t);})*
}

//tipo_variable: (tipo_variable_simple | tipo_variable_array)
/**
 * Params: 
 *	-t: lista de tokens de la enumeraci칩n de los identificadores de las variables
 *	-paramArray: creado y utilizado en la producci칩n "parametro_formal" para rellenar los datos de un par치metro ARRAY
 *	-paramClass: valor != de NONE en caso de ser un par치metro
 */
void tipo_variable(TypeAttrib typeAt) : 
{
	TypeAttrib typeAt1 = new TypeAttrib(), typeAt2 = new TypeAttrib();
	typeAt1.clone(typeAt); typeAt2.clone(typeAt);
	typeAt1.paramArray = null;
}
{
	( 
		tipo_variable_simple(typeAt1) 
		{ 
			typeAt.code.addBlock(typeAt1.code);
			typeAt.type = typeAt1.type;	// Si es un tipo simple, se asigna el tipo de la variable
		}
	|   tipo_variable_array(typeAt2)  
		{ 
			typeAt.code.addBlock(typeAt2.code); 
			typeAt.type = typeAt2.type;
		}
	)
}

//tipo_variable_array: <tARRAY> <tLPAREN> <tCONST_INT> <tRANGE> <tCONST_INT> <tRPAREN> <tOF> tipo_variable_simple
/*
	-t: lista de tokens de la enumeraci칩n de los identificadores de las variables
	-paramArray: necesario para heredar el valor de la producci칩n previa "tipo_variable"
	-paramClass: valor != de NONE en caso de ser un par치metro (heredado de "tipo_variable")
 */
void tipo_variable_array(TypeAttrib typeAt) : 
{
	ArrayTypeAttrib arrayTypeAt = new ArrayTypeAttrib();
	boolean isParameter = (typeAt.paramClass != Symbol.ParameterClass.NONE);
	boolean isDeclaration = !isParameter;
	TypeAttrib typeAt2;
}
{
	<tARRAY> <tLPAREN>
	(<tMINUS> {arrayTypeAt.minus1 = -1;} | <tPLUS>)? arrayTypeAt.tMin = <tCONST_INT> 
		<tRANGE> 
	(<tMINUS> {arrayTypeAt.minus2 = -1;} | <tPLUS>)? arrayTypeAt.tMax = <tCONST_INT> 
	<tRPAREN>
	{
		arrayTypeAt.min = Integer.parseInt(arrayTypeAt.tMin.image) * arrayTypeAt.minus1;
		arrayTypeAt.max = Integer.parseInt(arrayTypeAt.tMax.image) * arrayTypeAt.minus2;
		semantic.emptyArrayRange(arrayTypeAt.tMax, arrayTypeAt.min, arrayTypeAt.max); // Comprobar que el rango no es vac칤o (min > max)

		if (isDeclaration) {
			arrayTypeAt.baseArray = new SymbolArray("", arrayTypeAt.min, arrayTypeAt.max, Symbol.Types.UNDEFINED, typeAt.paramClass);
			/* baseArray se crea aqu칤 para que en "tipo_variable_simple" se pueda
			   insertar en la tabla de s칤mbolos con los valores de "min" y "max" */
		}
	}
	<tOF> { typeAt2 = new TypeAttrib(); typeAt2.clone(typeAt); typeAt2.paramArray = arrayTypeAt.baseArray; }
	tipo_variable_simple(typeAt2)
	{arrayTypeAt.baseType = typeAt2.type;}
	{
		if (isParameter){ // Par치metro ARRAY
			// paramArray se ha creado en "parametro_formal" para rellenarse aqu칤
			typeAt.paramArray.minInd = arrayTypeAt.min;
			typeAt.paramArray.maxInd = arrayTypeAt.max;
			typeAt.paramArray.parClass = typeAt.paramClass;
			typeAt.paramArray.baseType = arrayTypeAt.baseType;
		}
		typeAt.type = Symbol.Types.ARRAY;
	}
}

//tipo_variable_simple: <tINT> | <tCHAR> | <tBOOL>
/*
	-t: lista de tokens de la enumeraci칩n de los identificadores de las variables
	-paramArray: creado en "tipo_variable_array" para rellenar datos del array (칤ndices y paramClass). 
				En esta producci칩n se termina de rellenar (nombre) y se inserta en la tabla de s칤mbolos.
	-paramClass: valor != de NONE en caso de ser un par치metro
 */
void tipo_variable_simple(TypeAttrib typeAt) : 
{
	boolean isArray = (typeAt.paramArray != null);
	/* Si paramArray != de null, estamos en una declaraci칩n de
	   array y contiene los valores de min, max y paramClass */
}
{
	( <tINT>  { typeAt.type = Symbol.Types.INT; }
	| <tCHAR> { typeAt.type = Symbol.Types.CHAR; }
	| <tBOOL> { typeAt.type = Symbol.Types.BOOL; }
	)
	{
		if (typeAt.t == null) { // Par치metros
			/* Si no hay tokens, devolver el tipo de la variable porque se est치n leyendo par치metros
			   y estos se insertan en la st en 'declaracion_procedimiento' y 'declaracion_funcion' */
			return;
		}
		else if (isArray) { // Declaraci칩n de variables arrays
			typeAt.paramArray.baseType = typeAt.type;
			for (Token id : typeAt.t) { 
				SymbolArray newArray = typeAt.paramArray.clone(); // Necesario para obtener un array nuevo con la info de 칤ndices y paramClass
				newArray.name = id.image;
				semantic.insertSymbol(id, newArray, null);
			}
		} else { // Declaraci칩n de variables simples
			for (Token id : typeAt.t) {
				/*游꾺 Hacer funci칩n de generaci칩n de c칩digo que realice:
					- Aumentar espacio requerido
					- Asignar direcci칩n (lugar en memoria)
				*/
				switch (typeAt.type) {
					case INT:  semantic.insertSymbol(id, new  SymbolInt(id.image, typeAt.paramClass), null); break;
					case CHAR: semantic.insertSymbol(id, new SymbolChar(id.image, typeAt.paramClass), null); break;
					case BOOL: semantic.insertSymbol(id, new SymbolBool(id.image, typeAt.paramClass), null); break;
				}
			}
		}
	}
}

//declaracion_procedimiento: <tPROCDURE> <tID> (<tLPAREN> lista_parametros <tRPAREN>)? <tIS> (declaracion_var)* (declaracion_procedimiento | declaracion_funcion)* <tBEGIN> (instruccion)+ <tEND> <tSEMICOLON>
void declaracion_procedimiento(Attributes at) : 
{
	FuncProcDecAttrib funcProcDecAt = new FuncProcDecAttrib();
	Attributes at1 = new Attributes();
	String label;
}
{
	{ 
		label = CGUtils.newLabel(); 
		at1.code.addLabel(label);
	}
	<tPROCEDURE>
	funcProcDecAt.id = <tID> // Puede NO tener par치metros
	( <tLPAREN> lista_parametros(funcProcDecAt) <tRPAREN>)?
 	{

		semantic.insertSymbol(funcProcDecAt.id, new SymbolProcedure(funcProcDecAt.id.image, new ArrayList<Symbol>(funcProcDecAt.params.values())), label);
		semantic.insertBlock();

		int params_size = funcProcDecAt.params.size();
		if (params_size > 0) {
				// Se insertan params con los tokens en la tabla de s칤mbolos DESPU칄S de crear un nuevo bloque
			for (Map.Entry<Token, Symbol> entry : funcProcDecAt.params.entrySet()) {
				semantic.insertSymbol(entry.getKey(), entry.getValue(), null);
			}

			// Desapilado de par치metros en orden inverso
			// level: n칰mero de bloques que hay que bajar para obtener la variable
			int level = 0; // TODO: nivel fijado a 0, no s칠 si es lo correcto
			for (int i = params_size; i > 0; i--) {
				at1.code.addInst(PCodeInstruction.OpCode.SRF, level, i+2);
				at1.code.addInst(PCodeInstruction.OpCode.ASGI);
			}
		}
	}
	<tIS>
		( declaracion_var(at1) )*
		( declaracion_procedimiento(funcProcDecAt) | declaracion_funcion(funcProcDecAt) )*
	<tBEGIN>
		{ 
			label = CGUtils.newLabel();

			at1.code.addInst(PCodeInstruction.OpCode.JMP, label); // Salto a la instrucci칩n de inicio del procedimiento
			at1.code.addLabel(label); // Etiqueta de inicio del procedimiento
		} 
		(instruccion(at1))+
	<tEND> <tSEMICOLON>
	{
		if (verbose) semantic.printSymbolTable(funcProcDecAt.id.image); // Impresi칩n de la tabla de s칤mbolos
		semantic.removeBlock(); // Eliminaci칩n del bloque actual
		at.code.addBlock(at1.code);
		at.code.addBlock(funcProcDecAt.code);
		at.code.addInst(PCodeInstruction.OpCode.CSF);
	}
}

//declaracion_funcion: <tFUNCTION> <tID> ( <tLPAREN> lista_parametros <tRPAREN> )? <tRETURN> tipo_variable_simple <tIS> (declaracion_var)* (declaracion_procedimiento | declaracion_funcion)* <tBEGIN> (instruccion)+ <tEND> <tSEMICOLON>
void declaracion_funcion(Attributes at) : 
{
	TypeAttrib typeAt = new TypeAttrib();
	FuncProcDecAttrib funcProcDecAt = new FuncProcDecAttrib();
	Attributes at1 = new Attributes();
	String label;
}
{
	{
		label = CGUtils.newLabel();
		at1.code.addLabel(label);
	}
	<tFUNCTION>
	funcProcDecAt.id = <tID>
	( <tLPAREN> lista_parametros(funcProcDecAt) <tRPAREN> )? // Puede NO tener par치metros
	<tRETURN> {typeAt.t = null;} // Indicar que se est치n leyendo par치metros, y que los s칤mbolos se insertan aqu칤 y no en "tipo_variable_simple"
	tipo_variable_simple(typeAt)
	{funcProcDecAt.returnType = typeAt.type;}
	{
		funcProcDecAt.functionSymbol = new SymbolFunction(funcProcDecAt.id.image, new ArrayList<Symbol>(funcProcDecAt.params.values()), funcProcDecAt.returnType);
		semantic.insertSymbol(funcProcDecAt.id, funcProcDecAt.functionSymbol, label);
		semantic.insertBlock();

		int params_size = funcProcDecAt.params.size();
		if (params_size > 0) {
				// Se insertan params con los tokens en la tabla de s칤mbolos DESPU칄S de crear un nuevo bloque
			for (Map.Entry<Token, Symbol> entry : funcProcDecAt.params.entrySet()) {
				semantic.insertSymbol(entry.getKey() ,entry.getValue(), null);
			}

				// Desapilado de par치metros en orden inverso
			// level: n칰mero de bloques que hay que bajar para obtener la variable
			int level = 0; // TODO: nivel fijado a 0, no s칠 si es lo correcto
			for (int i = params_size; i > 0; i--) {
				at1.code.addInst(PCodeInstruction.OpCode.SRF, level, i+2);
				at1.code.addInst(PCodeInstruction.OpCode.ASGI);
			}
		}

	}
	<tIS>
		(declaracion_var(at))*
		( declaracion_procedimiento(funcProcDecAt) | declaracion_funcion(funcProcDecAt) )*
	<tBEGIN> 
		{
			semantic.enterFunction(funcProcDecAt.functionSymbol);

			label = CGUtils.newLabel();
			at1.code.addInst(PCodeInstruction.OpCode.JMP, label); // Salto a la instrucci칩n de inicio de la funci칩n
			at1.code.addLabel(label); // Etiqueta de inicio de la funci칩n
		}
		(instruccion(at1))+
	<tEND> <tSEMICOLON>
	{
		semantic.exitFunction(getToken(0));
		if (verbose) semantic.printSymbolTable(funcProcDecAt.id.image); // Impresi칩n de la tabla de s칤mbolos
		semantic.removeBlock(); // Eliminaci칩n del bloque actual
		at.code.addBlock(at1.code);
		at.code.addBlock(funcProcDecAt.code);
		at.code.addInst(PCodeInstruction.OpCode.CSF);
	}
}


//lista_parametros: parametro_formal (<tSEMICOLON> parametro_formal)*
void lista_parametros(FuncProcDecAttrib funcProcDecAt) : {}
{
	parametro_formal(funcProcDecAt) 
	( <tSEMICOLON> parametro_formal(funcProcDecAt) )*
}

//parametro_formal: lista_ids <tCOLON> <tREF>? tipo_variable
void parametro_formal(FuncProcDecAttrib funcProcDecAt) : 
{
	ParVarAttrib parVarAt = new ParVarAttrib();
	TypeAttrib typeAt = new TypeAttrib();
}
{
	lista_ids(parVarAt.t)
	<tCOLON>
	( <tREF> {parVarAt.paramClass = Symbol.ParameterClass.REF;} )?
	{
		typeAt.t = null;
		typeAt.paramArray = parVarAt.paramArray;
		typeAt.paramClass = parVarAt.paramClass;
	}
	tipo_variable(typeAt)
	// Aqu칤 es el 칰nico lugar donde se utiliza 'paramArray' ya que aqu칤 se leen par치metros
	// Funciona de manera que en "tipo_variable_array" al ver que 'paramClass' no es NONE (es VAL o REF), se rellenan los datos de 'paramArray' con: min, max y paramClass
	{
		parVarAt.baseType = typeAt.type;

		for (Token id : parVarAt.t) {
		// Comprobar si hay alg칰n par치metro con el mismo nombre (se hace tras declarar todos los par치metros)
			System.out.println("ID: " + id.image);

			if (parVarAt.baseType == Symbol.Types.ARRAY) { // Par치metros array
				SymbolArray newArray = typeAt.paramArray.clone();
				newArray.name = id.image;
				funcProcDecAt.params.put(id, newArray); // Insertado de par치metro array en el diccionario de par치metros
			} else { // Par치metros simples
				switch (parVarAt.baseType) {
					// Insertado de par치metro (int|char|bool) en el diccionario de par치metros
					case INT:  funcProcDecAt.params.put(id, new  SymbolInt(id.image, parVarAt.paramClass)); break;
					case CHAR: funcProcDecAt.params.put(id, new SymbolChar(id.image, parVarAt.paramClass)); break;
					case BOOL: funcProcDecAt.params.put(id, new SymbolBool(id.image, parVarAt.paramClass)); break;
				}
			}
		}
		// System.out.println("Lista de par치metros: " + funcProcDecAt.params);

	}
}


/*------------------------------------------- INSTRUCCIONES -------------------------------------------*/ 

//instruccion: (inst_leer | <tSKIP_LINE> | inst_escribir | inst_escribir_linea | inst_invocacion_procedimiento_o_asignacion | inst_if | inst_while | <tNULL> | inst_return) <tSEMICOLON>
void instruccion(Attributes at): 
{
	Attributes at1 = new Attributes();
}
{
	(	inst_leer(at1) {at.code.addBlock(at1.code);}
	|	<tSKIP_LINE>  skip_line(at1) {at.code.addBlock(at1.code);}
	|   inst_escribir(at1) {at.code.addBlock(at1.code);}
	|   inst_escribir_linea(at1) {at.code.addBlock(at1.code);}
	|   inst_invocacion_procedimiento_o_asignacion(at1) {at.code.addBlock(at1.code);}
	|   inst_if(at1) {at.code.addBlock(at1.code);} // TODO
	|   inst_while(at1) {at.code.addBlock(at1.code);} // TODO
	|   inst_return(at1) {at.code.addBlock(at1.code);}
	|   inst_exit(at1) {at.code.addBlock(at1.code);}
	|   <tNULL> )
	<tSEMICOLON>
}


void skip_line(Attributes at) : 
{
	long last_dir;
}
{
	{
		last_dir = semantic.getLastSymbolAddress();
		// System.out.println("Direcci칩n de almacenamiento temporal: " + last_dir);
		String label = CGUtils.newLabel();
		at.code.addLabel(label);
		at.code.addInst(PCodeInstruction.OpCode.SRF, 0, (int) last_dir); // Direcci칩n de almac칠n temporal (var local)
		at.code.addInst(PCodeInstruction.OpCode.RD, 0); // Leer de stdin y asignarlo a var_local
		at.code.addInst(PCodeInstruction.OpCode.SRF, 0, (int) last_dir); 
		at.code.addInst(PCodeInstruction.OpCode.DRF); // Obtener valor que se acaba de escribir
		at.code.addInst(PCodeInstruction.OpCode.STC, 10);
		at.code.addInst(PCodeInstruction.OpCode.EQ); // Compararlo con el salto de l칤nea
		at.code.addInst(PCodeInstruction.OpCode.JMF, label); // Repetir si no es as칤
	}
}

//inst_leer: <tGET> <tLPAREN> inst_leer_elemento ( <tCOMMA> inst_leer_elemento )* <tRPAREN>
void inst_leer(Attributes at) : 
{
	Attributes at1 = new Attributes();
}
{
	<tGET> // Necesita par치metros
	<tLPAREN> 
	inst_leer_elemento(at1)
	( <tCOMMA> inst_leer_elemento(at1) )* 
	<tRPAREN>
	{
		at.code.addBlock(at1.code);
	}
}

//inst_leer_elemento: <tID> ( array_access )?
void inst_leer_elemento(Attributes at) :
{
	Attributes at1 = new Attributes();
	Token id;
	boolean access = false;
	/* Se deben leer variables asignables de tipo integer o character.
	   Solo son asignables las variables simples y los elementos de un array */
}
{
	id = <tID> ( array_access(at1) {access = true;} )?
	{
		if (! semantic.isSymbolDefined(id)) return; // El error de s칤mbolo no definido est치 en semantic.isSymbolDefined
		Symbol symbol = semantic.getSymbol(id);

		semantic.isProcedureOrFunction(id, symbol); // si es procedimiento o funci칩n, error

		/* Comprobaciones relacionadas con arrays:
		- Si es array: 
			- comprobar que se ha accedido a un elemento y no al array entero
			- comprobar que el tipo del array es INT o CHAR
		- Si no: que no se acceda a una variable simple, procedimiento o funci칩n como a un array
		*/
		semantic.readingArrayChecks(id, symbol, access);

		semantic.isBooleanBeingRead(id, symbol); // no se puede leer un booleano

		{ 
			at.code.addBlock(at1.code);

			// System.out.println("Leyendo " + id.image + "con nivel: " + symbol.nivel + " y direcci칩n: " + symbol.dir);
			at.code.addInst(PCodeInstruction.OpCode.SRF, symbol.nivel, (int) symbol.dir);
			at.code.addInst(PCodeInstruction.OpCode.RD, symbol.type == Symbol.Types.CHAR ? 0 : 1);
		}
	}
}

//array_access: <tLPAREN> expresion <tRPAREN>
void array_access(Attributes at) : 
{
	ExpressionAttrib expAt = new ExpressionAttrib();
}
{
	<tLPAREN> 
	expresion(expAt)
	{
		semantic.indexIsInteger(getToken(0), expAt.type); // Comprobar que el 칤ndice es de tipo INT
		// No hace falta comprobar que el 칤ndice est칠 dentro del rango del array porque no se pide
	}
	<tRPAREN> 
	{ at.code.addBlock(expAt.code); }
}

//inst_escribir: <tPUT> <tLPAREN> inst_escribir_elemento ( <tCOMMA> inst_escribir_elemento )* <tRPAREN>
void inst_escribir(Attributes at) : 
{
	Attributes at1 = new Attributes();
}
{
	<tPUT> // Necesita par치metros
	<tLPAREN> 
	inst_escribir_elemento(at1)
	( <tCOMMA> inst_escribir_elemento(at1) )*
	<tRPAREN>
	{at.code.addBlock(at1.code);}
}


//inst_escribir_linea: <tPUT_LINE> (<tLPAREN> inst_escribir_elemento (<tCOMMA> inst_escribir_elemento)* <tRPAREN>)?
void inst_escribir_linea(Attributes at) :
{
	Boolean hasString = false;
	Attributes at1 = new Attributes();
}
{
	<tPUT_LINE> // Puede NO tener par치metros
	(
		<tLPAREN> inst_escribir_elemento(at1)
		( <tCOMMA> inst_escribir_elemento(at1) )* <tRPAREN>
		{ hasString = true; }
	)?
	{
		if (hasString) {
			at.code.addBlock(at1.code);
		}

		// A침adir siempre el salto de l칤nea: CR + LF (asciis 13 y 10 respectivamente)
		at.code.addInst(PCodeInstruction.OpCode.STC, 13);
		at.code.addInst(PCodeInstruction.OpCode.WRT, 0);
		at.code.addInst(PCodeInstruction.OpCode.STC, 10);
		at.code.addInst(PCodeInstruction.OpCode.WRT, 0);
	}
}


//inst_escribir_elemento: <tCONST_STRING> | expresion
void inst_escribir_elemento(Attributes at) : 
{
	ExpressionAttrib expAt = new ExpressionAttrib();
	expAt.type = Symbol.Types.STRING; // default value
	Token value;
	// Se pueden escribir constantes string y expresiones de tipo integer, character y boolean, incluidos los elementos de un array
}
{
	(value = <tCONST_STRING>
		{
			// Iterar en el valor del string y escribir uno a uno los ascii de los caracteres
			for (int i = 0; i < value.image.length(); i++) {
				expAt.code.addInst(PCodeInstruction.OpCode.STC, value.image.charAt(i));
				expAt.code.addInst(PCodeInstruction.OpCode.WRT, 0);
			}
			at.code.addBlock(expAt.code);
		} 
	 | expresion(expAt)
	 {
		at.code.addBlock(expAt.code);
		at.code.addInst(PCodeInstruction.OpCode.WRT, expAt.type == Symbol.Types.CHAR ? 0 : 1);
	 }
	)
	{
		switch (expAt.type) {
			case INT:
			case BOOL:
			case CHAR:
			case STRING: break;
			case ARRAY: semantic.error(getToken(0), "No se puede escribir un array entero, pero s칤 componentes de array."); break;
			default: semantic.error(getToken(0), 
					"Solo se pueden escribir valores de tipo " + tokenImage[tINT] + ", " + tokenImage[tCHAR] + ", " + tokenImage[tBOOL] + " y " + tokenImage[tCONST_STRING] + ".");
		}
	}
}


//inst_invocacion_procedimiento_o_asignacion: <tID> (array_access)? (<tASSIGN> expresion | <tLPAREN> lista_una_o_mas_exps <tRPAREN>)?
void inst_invocacion_procedimiento_o_asignacion(Attributes at) : 
{
	Attributes at1 = new Attributes();
	ExpressionAttrib expAt = new ExpressionAttrib();
	Token id;
	boolean access = false;
}
{
	id = <tID>
	(   // asignacion()
		LOOKAHEAD( (array_access(at1))? <tASSIGN> ) // comprueba si es una asignaci칩n (mediante uso de LOOKAHEAD sint치ctico)
		(array_access(at1) {access = true;} )? 
		<tASSIGN>
		expresion(expAt)
		{
			if (! semantic.isSymbolDefined(id)) return; // Si no est치 definido, no se puede comprobar nada m치s
			Symbol symbol = semantic.getSymbol(id);

			/* Comprobaciones relacionadas con arrays:
			- Si es array:
				- se debe acceder a un elemento (el array no es asignable pero sus elementos s칤 lo son)
				- comprobar que el tipo de la expresi칩n coincide con el tipo base del array
			- Si no: no puede accederse a una variable no array
			*/
			semantic.assigningArrayChecks(id, symbol, expAt.type, access);

			/* Comprobaciones sobre los tipos de los asignables:
			- Si es un procedimiento o funci칩n, error
			- Asignable y expresi칩n deben ser del mismo tipo
			*/
			semantic.assignableTypeChecks(id, symbol, expAt.type); // Solamente son asignables las variables simples y los elementos de un array

			{
				at.code.addInst(PCodeInstruction.OpCode.SRF, symbol.nivel, (int) symbol.dir);
				at.code.addBlock(expAt.code);
				at.code.addBlock(at1.code);
				at.code.addInst(PCodeInstruction.OpCode.ASG);
			}
		}
		| // invocaci칩n_procedimiento()
		// checkear que el id es un procedimiento y nada m치s
		( <tLPAREN> lista_una_o_mas_exps(id, at1) <tRPAREN> )? 
		{
			if (! semantic.isSymbolDefined(id)) return; // Si no est치 definido, no se puede comprobar nada m치s
			Symbol symbol = semantic.getSymbol(id);

			semantic.procedureChecks(id, symbol); // Comprobar que el s칤mbolo es un procedimiento, y que no es el procedimiento principal

			{
				// apilar los par치metros
				at.code.addBlock(expAt.code);
				at.code.addBlock(at1.code);

				// addOSFInst (int s, int l, String label) --> respetando 's' componentes del bloque actual, ocultando 'l' bloques de activaci칩n, comenzando la ejecuci칩n en 'label'
				String label = semantic.getLabelFromSymbol(id);
				at.code.addOSFInst((int) (semantic.getLastSymbolAddress()+1), symbol.nivel, label); // TODO: REVISAR PAR츼METROS DE OSF (creo que 's' y 'label' est치n ya bien)
			}
		}
	)
}

//inst_if: <tIF> expresion <tTHEN> instruccion+ (<tELSIF> expresion <tTHEN> instruccion+)* (<tELSE> instruccion+)? <tEND> <IF>
void inst_if(Attributes at) :
{
	ExpressionAttrib expAt = new ExpressionAttrib();
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	Attributes at1 = new Attributes();
	Attributes at2 = new Attributes();
	Attributes at3 = new Attributes();
	Boolean hasElsif = false;
}
{
	{ 
		String label_elsif = CGUtils.newLabel();
	  	String label_end = CGUtils.newLabel();
		String label_else = CGUtils.newLabel(); 
	}
	<tIF> expresion(expAt) 
	{
		semantic.ifChecks(getToken(0), expAt.type); // Comprobar que la guarda es de tipo BOOL
	}
	<tTHEN> ( instruccion(at1) )+
	(<tELSIF> expresion(expAt1)
	{
		hasElsif = true;
		semantic.ifChecks(getToken(0), expAt1.type); // Comprobar que la guarda es de tipo BOOL
	} <tTHEN> ( instruccion(at2) )+ )* 
	(<tELSE>  ( instruccion(at3) )+ )? 
	<tEND> <tIF>
	{
		/* Backup de If-else funcional:
			at.code.addBlock(expAt.code); // If: Guarda 
			at.code.addInst(PCodeInstruction.OpCode.JMF, label_else); // Salto a Else si la guarda es falsa
			at.code.addBlock(at1.code);	// If: Instrucciones
			at.code.addInst(PCodeInstruction.OpCode.JMP, label_end); // Salto al final del if
			at.code.addLabel(label_else); // Else
			at.code.addBlock(at3.code); // Else: Instrucciones
			at.code.addLabel(label_end); // Fin del if
		*/

		// Elsif (1 nivel) funcional. TODO: hacer recursivo para m치s niveles
		at.code.addBlock(expAt.code); // If: Guarda 
		if (hasElsif) at.code.addInst(PCodeInstruction.OpCode.JMF, label_elsif); // Salto a Elsif si la guarda es falsa
		at.code.addBlock(at1.code);	// If: Instrucciones
		at.code.addInst(PCodeInstruction.OpCode.JMP, label_end); // Salto al final del if
		at.code.addLabel(label_elsif); // Elsif
		at.code.addBlock(expAt1.code); // Elsif: Guarda
		at.code.addInst(PCodeInstruction.OpCode.JMF, label_else); // Salto a Else si la guarda es falsa
		at.code.addBlock(at2.code); // Elsif: Instrucciones
		at.code.addInst(PCodeInstruction.OpCode.JMP, label_end); // Salto al final del if
		at.code.addLabel(label_else); // Else
		at.code.addBlock(at3.code); // Else: Instrucciones
		at.code.addLabel(label_end); // Fin del if
	}
}

//inst_while: <tWHILE> expresion <tLOOP> instruccion+ <tENDLOOP>
void inst_while(Attributes at) : 
{
	ExpressionAttrib expAt = new ExpressionAttrib();
	Attributes at1 = new Attributes();
}
{
	<tWHILE> expresion(expAt)
	{
		semantic.whileChecks(getToken(0), expAt.type); // Comprobar que la guarda es de tipo BOOL
	}
	<tLOOP> ( instruccion(at1) )+ <tEND> <tLOOP>
	{ 
		// TODO: REVISAR QUE FUNCIONE
		at.code.addBlock(expAt.code);
		at.code.addBlock(at1.code);
	}
}

//inst_return: <tRETURN> expresion
void inst_return(Attributes at) : 
{
	ExpressionAttrib expAt = new ExpressionAttrib();
	Token t;
}
{
	t = <tRETURN> expresion(expAt) 
	{
		semantic.inst_return(t, expAt.type);
		at.code.addBlock(expAt.code);
		at.code.addInst(PCodeInstruction.OpCode.CSF); // A침adida siempre tras un return
	}
}

//inst_exit: <tEXIT>
void inst_exit(Attributes at) : {}
{
	<tEXIT>
	{ at.code.addInst(PCodeInstruction.OpCode.LVP); }
}


/*--------------------------------------- EXPRESIONES ---------------------------------------*/ 

//expresion: relacion ( (<tAND> relacion)+ | (<tOR> relacion)+ )?
/** 游꾺 REVISAR MAIL QUE MAND칍 (20/4) SOBRE FALLO EN ESTA PRODUCCI칍N
 * Params:
 * -param: != de null si es un par치metro de funci칩n/procedimiento, o bien una componente de un vector
 * -esAsignable: array de un 칰nico booleano para comprobar si la expresi칩n es asignable. Debe ser un array
 * 				 para ser un elemento mutable y poder modificar su valor en las producciones que lo necesiten.
 */
void expresion(ExpressionAttrib expAt) : 
{
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	ExpressionAttrib expAt2 = new ExpressionAttrib();
	Symbol.Types type;
	boolean moreThanOne = false;
	String errorMsg = "Las relaciones de una expresi칩n l칩gica deben ser de tipo " + tokenImage[tBOOL] + ".";
	Token op = new Token();
}
{
	relacion(expAt1)
	(
		( op = <tAND>
			{
				moreThanOne = true;
				if (expAt1.type != Symbol.Types.BOOL)
					semantic.error(getToken(0), errorMsg);
			}
			relacion(expAt2) 
			{
				// Si hay m치s de una relaci칩n, deben ser todas BOOL
				if (expAt2.type != Symbol.Types.BOOL)
					semantic.error(getToken(0), errorMsg);
			}
		)+
		| 
		( op = <tOR>
			{
				moreThanOne = true;
				if (expAt1.type != Symbol.Types.BOOL)
					semantic.error(getToken(0), errorMsg);
			}
			relacion(expAt2)
			{
				// Si hay m치s de una relaci칩n, deben ser todas BOOL
				if (expAt2.type != Symbol.Types.BOOL)
					semantic.error(getToken(0), errorMsg);
			}
		)+
	)?
	{
		expAt.code.addBlock(expAt1.code); // Subir c칩digo hacia llamada superior

		if (moreThanOne && expAt1.esAsignable != null) expAt1.esAsignable = false; // Si hay m치s de una relaci칩n, la expresi칩n no es asignable
		expAt.type = (moreThanOne) ? Symbol.Types.BOOL : expAt1.type;

		if (moreThanOne) {
			// apilar operador l칩gico
			switch (op.kind) {
				case tAND: expAt.code.addInst(PCodeInstruction.OpCode.AND); break;
				case tOR:  expAt.code.addInst(PCodeInstruction.OpCode.OR); break;
			}
		}
	}
}

//relacion: expresion_simple (operador_relacional expresion_simple)?
/**
 * Params: an치logo a los de la producci칩n "expresion"
 */
void relacion(ExpressionAttrib expAt) : 
{
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	ExpressionAttrib expAt2 = new ExpressionAttrib();
	Token op = new Token();
}
{
	expresion_simple(expAt1)
 	( op = operador_relacional() expresion_simple(expAt2) )?
	{
		expAt.code.addBlock(expAt1.code); // Subir c칩digo hacia llamada superior

		// Puede haber una o dos expresiones, pero si hay dos, deben ser del mismo tipo
		if (expAt2.type != null) {
			if (expAt1.esAsignable != null) expAt1.esAsignable = false; // Si hay dos expresiones, la expresi칩n no es asignable
			if (expAt1.type != expAt2.type)
				semantic.error(getToken(0), "Las expresiones de una relaci칩n deben ser del mismo tipo, se encontr칩 " + expAt1.type + " y " + expAt2.type + ".");

			expAt.type = Symbol.Types.BOOL; // Si hay dos expresiones, el resultado es booleano
		}
		// else: si hay solo una expresi칩n, se devuelve su tipo
		else { expAt.type = expAt1.type; }

		// Si hay dos expresiones, entonces apilar el c칩digo de la segunda y el operador relacional
		if (expAt2.type != null) {
			expAt.code.addBlock(expAt2.code);
			// apilar operador relacional
			switch (op.kind) {
				case tEQ:  expAt.code.addInst(PCodeInstruction.OpCode.EQ); break;
				case tLT:  expAt.code.addInst(PCodeInstruction.OpCode.LT); break;
				case tGT:  expAt.code.addInst(PCodeInstruction.OpCode.GT); break;
				case tLE:  expAt.code.addInst(PCodeInstruction.OpCode.LTE); break;
				case tGE:  expAt.code.addInst(PCodeInstruction.OpCode.GTE); break;
				case tNEQ: expAt.code.addInst(PCodeInstruction.OpCode.NEQ); break;
			}
		}
	}
}

//operador_relacional: (<tEQ> | <tLT> | <tGT> | <tLE> | <tGE> | <tNEQ>)
Token operador_relacional() : 
{
	Token op = new Token();
}
{
	(op = <tEQ> | op = <tLT> | op = <tGT> | op = <tLE> | op = <tGE> | op = <tNEQ>)
	{ return op; }
}

//expresion_simple: ( <tPLUS> |	<tMINUS> )? termino ( ( <tPLUS> | <tMINUS> ) termino )*
/**
 * Params: an치logo a los de la producci칩n "expresion"
 */
void expresion_simple(ExpressionAttrib expAt) :
{
	boolean sign = false;
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	ExpressionAttrib expAt2 = new ExpressionAttrib();
	Token op = new Token();
}
{
    (( op = <tPLUS> | op = <tMINUS>) { sign = true; } )? termino(expAt1) 
		{
			semantic.signInExpressionCheck(getToken(0), expAt1.type, sign); // Si hay signo, la expresi칩n debe ser de tipo INT
			if (op.kind == tMINUS) expAt1.code.addInst(PCodeInstruction.OpCode.NGI); // Si hay signo negativo, a침adir instrucci칩n de negaci칩n
		}
	(( op = <tPLUS> | op = <tMINUS> ) termino(expAt2)
		{
			sign = true;
			semantic.moreThanOneExpressionCheck(getToken(0), expAt2.type); // Si hay m치s de un t칠rmino, deben ser todos INT
		}
	)*
	{
		expAt.code.addBlock(expAt1.code); // Subir c칩digo hacia llamada superior

		if (sign && expAt1.esAsignable != null) expAt1.esAsignable = false; // Si hay signo o m치s de un t칠rmino, la expresi칩n no es asignable
		expAt.type = (sign) ? Symbol.Types.INT : expAt1.type;

		// Si hay m치s de un t칠rmino, a침adir el c칩digo del segundo t칠rmino y apilar el operador
		if (expAt2.type != null) {
			expAt.code.addBlock(expAt2.code);
			switch (op.kind) {
				case tPLUS: expAt.code.addInst(PCodeInstruction.OpCode.PLUS); break;
				case tMINUS: expAt.code.addInst(PCodeInstruction.OpCode.SBT); break;
			}
		}
	}
}
                                                                                                                                                                         
//termino: factor (operador_multiplicativo factor)*
/**
 * Params: an치logo a los de la producci칩n "expresion"
 */
void termino(ExpressionAttrib expAt) : 
{
	boolean moreThanOne = false;
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	ExpressionAttrib expAt2 = new ExpressionAttrib();
	Token op = new Token();
}
{
    factor(expAt1) 
	( op = operador_multiplicativo()
		{
			moreThanOne = true;
			semantic.multiplicativeFactorCheck(getToken(0), expAt1.type); // Los factores deben ser todos INT para poder operar con *, /, %
		}
	factor(expAt2)
		{ 
			// Si hay m치s de un factor, deben ser todos INT
			semantic.multiplicativeFactorCheck(getToken(0), expAt2.type); // Los factores deben ser todos INT para poder operar con *, /, %
		}
	)*
	{
		expAt.code.addBlock(expAt1.code); // Subir c칩digo hacia llamada superior

		if (moreThanOne && expAt1.esAsignable != null) expAt1.esAsignable = false; // Si hay m치s de un factor, la expresi칩n no es asignable
		expAt.type = (moreThanOne) ? Symbol.Types.INT : expAt1.type;

		// Si hay m치s de un factor, a침adir el c칩digo del segundo factor y apilar el operador
		if (expAt2.type != null) {
			expAt.code.addBlock(expAt2.code);

			switch (op.kind) {
				case tMULT: expAt.code.addInst(PCodeInstruction.OpCode.TMS); break;
				case tDIV:  expAt.code.addInst(PCodeInstruction.OpCode.DIV); break;
				case tMOD:  expAt.code.addInst(PCodeInstruction.OpCode.MOD); break;
			}
		}
	}
}

//operador_multiplicativo: (<tMULT> | <tDIV> | <tMOD>)
Token operador_multiplicativo() : 
{
	Token op = new Token();
}
{
	(op = <tMULT> | op = <tDIV> | op = <tMOD>) 
	{ return op; }
}

//factor: (<tNOT>)? primario
/**
 * Params: an치logo a los de la producci칩n "expresion"
 */
void factor(ExpressionAttrib expAt) : 
{
	// Symbol.Types type;
	boolean not = false;
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	// expAt1.clone(expAt);
}
{
	( <tNOT> {not = true;} )? primario(expAt1) 
	{
		expAt.code.addBlock(expAt1.code); // Subir c칩digo hacia llamada superior

		if (not) {
			if (expAt1.esAsignable != null) expAt1.esAsignable = false;
			semantic.notOperatorCheck(getToken(0), expAt1.type); // Operador 'not' solo se puede usar con variables de tipo BOOL 

			expAt.code.addBlock(expAt1.code);
			expAt.code.addInst(PCodeInstruction.OpCode.NGB);

			expAt.type = Symbol.Types.BOOL;
		}
		expAt.type = expAt1.type;
	}
}

//primario: <tLPAREN> expresion <tRPAREN> | <tINT2CHAR> <tLPAREN> expresion <tRPAREN> | <tCHAR2INT> <tLPAREN> expresion <tRPAREN> 
//			| <tID> <tLPAREN> lista_una_o_mas_exps <tRPAREN> | <tID> | <tCONST_INT> | <tCONST_CHAR> | <tTRUE> | <tFALSE>
/**
 * Params: an치logo a los de la producci칩n "expresion"
 */
void primario(ExpressionAttrib expAt) : 
{
	ExpressionAttrib expAt1 = new ExpressionAttrib();
	Attributes at = new Attributes(); // TODO: revisar si hay que meter esto o el expAt1 en lista_una_o_mas_exps
	Symbol.Types type = Symbol.Types.UNDEFINED;
	Token id;
}
{
	(			    <tLPAREN> expresion(expAt1) <tRPAREN> {expAt.code.addBlock(expAt1.code);}
	|	<tINT2CHAR> <tLPAREN> expresion(expAt1) <tRPAREN> 
	{
		expAt.code.addBlock(expAt1.code);
		semantic.int2charCheck(getToken(0), expAt1.type); // La expresi칩n debe ser de tipo INT
		expAt1.type = Symbol.Types.CHAR;
	}
	|	<tCHAR2INT> <tLPAREN> expresion(expAt1) <tRPAREN>
	{
		expAt.code.addBlock(expAt1.code);
		// La expresio칩n debe ser de tipo CHAR
		semantic.char2intCheck(getToken(0), expAt1.type); // La expresi칩n debe ser de tipo CHAR
		expAt1.type = Symbol.Types.INT;
	}
	|	LOOKAHEAD (2) id = <tID> <tLPAREN> lista_una_o_mas_exps(id, at) <tRPAREN> //invocar func. (con par치metros) o comp. array
	{
		if (! semantic.isSymbolDefined(id)) expAt1.type = Symbol.Types.UNDEFINED;
		Symbol symbol = semantic.getSymbol(id);

		switch (symbol.type) {
			// Si es una funci칩n, devolver su tipo retorno
			case FUNCTION: expAt1.type = ((SymbolFunction) symbol).returnType; break;
			// Checkear que no es procedimiento 
			case PROCEDURE: semantic.procedureInPrimaryError(id, "funci칩n"); break;
			case ARRAY:
				expAt1.type = ((SymbolArray) symbol).baseType; // Si es un array, devolver su tipo base
				if (expAt.esAsignable != null) expAt.esAsignable = true;
				id.clone(expAt.param);
				break;
			default: expAt1.type = symbol.type;
		}

		// apilar los par치metros y llamar a OSF
		expAt.code.addBlock(at.code);
		String label = semantic.getLabelFromSymbol(id);
		expAt.code.addOSFInst((int) (semantic.getLastSymbolAddress()+1), symbol.nivel, label); // TODO: REVISAR PAR츼METROS DE OSF (creo que 's' y 'label' est치n ya bien)

	}
	|	id = <tID> // variable simple o func/proced sin par치metros
	{
		if (! semantic.isSymbolDefined(id)) expAt1.type = Symbol.Types.UNDEFINED;
		Symbol symbol = semantic.getSymbol(id);

		switch (symbol.type) {
			case FUNCTION: 
				SymbolFunction function = (SymbolFunction) symbol;
				semantic.functionParametersCheck(id, function); // Checkear que la funci칩n no tenga par치metros
				expAt1.type = function.returnType;
				String label = semantic.getLabelFromSymbol(id);
				expAt.code.addOSFInst((int) (semantic.getLastSymbolAddress()+1), symbol.nivel, label); // TODO: REVISAR PAR츼METROS DE OSF (creo que 's' y 'label' est치n ya bien)
				break;
			// Checkear que no es procedimiento
			case PROCEDURE: semantic.procedureInPrimaryError(id, "variable"); break; 
			// Si es un array y no se espera un par치metro, debe accederse a un elemento
			case ARRAY: semantic.arrayAccessCheck(id, expAt.param); 
			default:
				id.clone(expAt.param);
				if (expAt.esAsignable != null) expAt.esAsignable = true;
				expAt1.type = symbol.type;
				// int currentLevel = semantic.getCurrentLevel();
				int level = 0; // TODO: revisar el nivel (al igual que en 'declaracion_procedimiento')
				expAt.code.addInst(PCodeInstruction.OpCode.SRF, level, (int) symbol.dir);
				expAt.code.addInst(PCodeInstruction.OpCode.DRF);
		}
	}
	|	<tCONST_INT>  { // guardar el valor del int y hacer: STC (ascii del int)
			expAt1.type = Symbol.Types.INT; 
			expAt.code.addInst(PCodeInstruction.OpCode.STC, Integer.parseInt(getToken(0).image));
		} 
	|	<tCONST_CHAR> {// STC (ascii del char)
			expAt1.type = Symbol.Types.CHAR;
			expAt.code.addInst(PCodeInstruction.OpCode.STC, getToken(0).image.charAt(1));
		} 
	|	<tTRUE> { // STC 1
			expAt1.type = Symbol.Types.BOOL;
			expAt.code.addInst(PCodeInstruction.OpCode.STC, 1);
		} 
	|	<tFALSE> { // STC 0
			expAt1.type = Symbol.Types.BOOL;
			expAt.code.addInst(PCodeInstruction.OpCode.STC, 0);
		}
	)
	{
		// No estoy seguro si los STC con 游꾺 estar칤an bien, ya que no s칠 si en todos los casos
		// se deber칤a de hacer el STC o solo en algunos casos determinados
		expAt.type = expAt1.type;
	}
}

//lista_una_o_mas_exps: expresion() ( <tCOMMA> expresion() )*
/**
 * Params:
 * 	-id: Token de la funci칩n/procedimiento invocado, o del array accedido
 */
void lista_una_o_mas_exps(Token id, Attributes at) : 
{ // Se ejecuta en una invocaci칩n de funci칩n o procedimiento y en el acceso a un array en una expresi칩n
	List<Symbol.Types> types = new ArrayList<>();
	Map<Token, Boolean> args = new LinkedHashMap<>();

	if (! semantic.isSymbolDefined(id)) return; // Si no se ha definido el s칤mbolo (error sem치ntico previo en primario)
	Symbol symbol = semantic.getSymbol(id);
	
	ExpressionAttrib expAt = new ExpressionAttrib();
}
{
	{
		expAt.esAsignable = false;
		expAt.param = new Token(0);
	}
	expresion(expAt)
	{
		types.add(expAt.type);
		if (expAt.param.kind == 0) args.put(getToken(0), false);
		else 			 		   args.put(expAt.param, expAt.esAsignable);
		expAt.esAsignable = false;
		expAt.param = new Token(0);
	}
	( <tCOMMA> expresion(expAt) 
		{ // Iterate over all the parameters
			types.add(expAt.type);
			if (expAt.param.kind == 0) args.put(getToken(0), false); // Si no se ha sobrescrito t, se a침ade el token actual
			else 			 		   args.put(expAt.param, expAt.esAsignable);
			expAt.esAsignable = false;
			expAt.param = new Token(0);
		}
	)*
	{
		if (symbol.type == Symbol.Types.FUNCTION || symbol.type == Symbol.Types.PROCEDURE) {

			// Recuperar lista par치metros del Token 'id' 
			List<Symbol> parList = (symbol instanceof SymbolFunction) ? ((SymbolFunction) symbol).parList : ((SymbolProcedure) symbol).parList;

			// Comprobar que el n칰mero de argumentos coincide con el n칰mero de par치metros
			String msg = (symbol.type == Symbol.Types.FUNCTION) ? "de la funci칩n" : "del procedimiento";
			msg += " '" + id.image + "'";
			if (semantic.checkNumberOfArguments(id, parList.size(), types.size(), msg)) return; // Si no coinciden los par치metros, salir

			// Comprobar que los tipos de los argumentos coinciden con los tipos de los par치metros
			semantic.checkArgumentTypes(id, types, parList, args, msg);

		} else if (symbol.type == Symbol.Types.ARRAY) {
			/*Comprobaciones sobre el array
			- El acceso a un array debe tener un 칰nico 칤ndice
			- El 칤ndice de un array debe ser de tipo integer
			*/ 
			semantic.arrayInListOfExpressionCheck(id, types);
		} else { // Si id es un tipo simple, no puede accederse a un elemento
			semantic.error(id, "No se puede acceder a un elemento del s칤mbolo '" + id.image + "' por ser de tipo " + symbol.type + " y no " + tokenImage[tARRAY] + ".");
		}
		at.code.addBlock(expAt.code);
	}
}
